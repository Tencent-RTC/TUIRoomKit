group 'com.tencent.cloud.tuikit.flutter.rtcconferencetuikit'
version '1.0'
import org.gradle.util.VersionNumber

//获取local.properties配置文件
def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}
//获取flutter的sdk路径
def flutterRoot = localProperties.getProperty('flutter.sdk')
if (flutterRoot == null) {
    throw new GradleException("Flutter SDK not found. Define location with flutter.sdk in the local.properties file.")
}


buildscript {
    repositories {
        google()
        mavenCentral()
    }

    dependencies {
        VersionNumber currentGradleVersion = VersionNumber.parse(gradle.gradleVersion)
        if (currentGradleVersion.major >= 8) {
            classpath 'com.android.tools.build:gradle:8.0.2'
            classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.9.0"
        } else {
            classpath 'com.android.tools.build:gradle:4.2.1'
            classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.6.10"
        }
    }
}

rootProject.allprojects {
    repositories {
        google()
        mavenCentral()
    }
}

apply plugin: 'com.android.library'

android {
    namespace "com.tencent.cloud.tuikit.flutter.rtcconferencetuikit"
    compileSdk 31

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    defaultConfig {
        minSdkVersion 19
        multiDexEnabled = true
    }

    dependencies {
        testImplementation 'junit:junit:4.13.2'
        testImplementation 'org.mockito:mockito-core:5.0.0'
        implementation 'androidx.appcompat:appcompat:1.3.1'
        compileOnly files("$flutterRoot/bin/cache/artifacts/engine/android-arm/flutter.jar")
        compileOnly 'androidx.annotation:annotation:1.1.0'
        api "com.tencent.liteav.tuikit:tuicore:8.2.6325"
        implementation "com.github.bumptech.glide:glide:4.12.0"
        implementation "de.hdodenhof:circleimageview:3.1.0"
        implementation "androidx.constraintlayout:constraintlayout:1.1.3"
        implementation "androidx.appcompat:appcompat:1.0.0"
        implementation 'io.trtc.uikit:common:latest.release'
    }

    testOptions {
        unitTests.all {
            testLogging {
               events "passed", "skipped", "failed", "standardOut", "standardError"
               outputs.upToDateWhen {false}
               showStandardStreams = true
            }
        }
    }
}
